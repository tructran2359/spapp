apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
androidExtensions {
    experimental = true
}

android {

    signingConfigs {
        defaultConfig {
            keyAlias 'vmlalias'
            keyPassword 'vmlqais'
            storeFile file('../spapp_default_keystore.jks')
            storePassword 'vmlqais'
        }
    }

    compileSdkVersion 28

    defaultConfig {
        applicationId "com.spgroup.spapp"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 8
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "BASE_API", "\"http://checkdev.prod.acquia-sites.com\""
    }


    buildTypes {

        debug {

        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.defaultConfig
        }
    }

    flavorDimensions "default"

    productFlavors {

        staging {
            dimension "default"
            buildConfigField "String", "GA_TRACKING_ID", '"UA-125316984-1"'
        }

        production {
            dimension "default"
            buildConfigField "String", "GA_TRACKING_ID", '"UA-125469843-1"'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->

            def formattedDate = new Date().format('yyyyMMdd')

            def flavorName = variant.productFlavors[0].name

            def buildName = variant.buildType.name

            outputFileName = formattedDate + "_" + "SPApp_Android_" + flavorName + "_" + buildName + "_v" + defaultConfig.versionName + "-" + defaultConfig.versionCode + ".apk"
//            outputFileName = formattedDate + "_" + "SPApp_Android_" + buildName + "_v" + defaultConfig.versionName + "-" + defaultConfig.versionCode + ".apk"

        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Android KTX
    implementation("androidx.core:core-ktx:$rootProject.ktxVersion") {
        transitive = false
    }

    // Anko
    implementation "org.jetbrains.anko:anko-commons:$rootProject.ankoVersion"

    // Android Support (refactor to use AndroidX package later)
    implementation "com.android.support:appcompat-v7:$rootProject.supportVersion"
    implementation "com.android.support:design:$rootProject.supportVersion"
    implementation "com.android.support:cardview-v7:$rootProject.supportVersion"
    // ViewModel - LiveData (refactor to use AndroidX package later)
    implementation "android.arch.lifecycle:extensions:${rootProject.lifecycleVersion}"
    kapt "android.arch.lifecycle:compiler:${rootProject.lifecycleVersion}"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:${rootProject.okhttpLoggingVersion}"

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxkotlin:${rootProject.rxKotlinVersion}"
    implementation "io.reactivex.rxjava2:rxandroid:${rootProject.rxAndroidVersion}"
    // Dagger 2
    implementation "com.google.dagger:dagger:${rootProject.daggerVersion}"
    kapt "com.google.dagger:dagger-compiler:${rootProject.daggerVersion}"
    // ImageLoader
    implementation "com.github.bumptech.glide:glide:${rootProject.glideVersion}"
    kapt "com.github.bumptech.glide:compiler:${rootProject.glideVersion}"
    // Custom Font
    implementation "uk.co.chrisjenx:calligraphy:$rootProject.calligraphyVersion"
    // ViewPager Indicator
    implementation "me.relex:circleindicator:1.2.2@aar"
    // Rounded ImageView
    implementation 'com.makeramen:roundedimageview:2.3.0'
    // PDF Reader
    implementation 'com.joanzapata.pdfview:android-pdfview:1.0.4@aar'
    // Sliding Menu
    implementation 'com.jeremyfeinstein.slidingmenu:library:1.3@aar'
    // Gif ImageView
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.3'
    // Google Analytics
    implementation "com.google.android.gms:play-services-analytics:$rootProject.googlePlayServices"

    // Test
    testImplementation 'junit:junit:4.12'
}
